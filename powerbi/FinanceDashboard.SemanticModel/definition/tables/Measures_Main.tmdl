table Measures_Main
	lineageTag: 617d435f-aedc-4e5d-b2b9-51256964853c

	measure 'Budget Amount' =
			
			SUM(budgets[budget_amount])
		formatString: \$#,0.###############;(\$#,0.###############);\$#,0.###############
		lineageTag: b14aea9f-d578-4bf1-a101-039212fc5dbe

	measure 'Actual Amount' = SUM(transactions_with_category[amount])
		formatString: \$#,0;(\$#,0);\$#,0
		lineageTag: 385ea110-5d91-4ebf-8450-5865a466aab6

	measure Variance = [Actual Amount] - [Budget Amount]
		formatString: \$#,0;(\$#,0);\$#,0
		lineageTag: 7b43ded1-2798-4947-ad33-03d115da4c31

	measure 'Variance %' = DIVIDE([Variance], [Budget Amount])
		lineageTag: 8ba8b32b-bfd7-4769-96d3-d9b940c260af

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'CF Income' = SUM(monthly_cashflow[income])
		lineageTag: 1919880c-cc7c-4a25-8a6a-d13f5c85ac78

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'CF Spending' = -SUM(monthly_cashflow[spending])
		lineageTag: ff58ba96-e79c-43ba-b61e-886e3705bf6c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'CF Net' = SUM(monthly_cashflow[net_cashflow])
		lineageTag: e2518873-7bc7-4be3-ae02-bcf0ba400da2

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'NW Total' =
			
			IF(
			  HASONEVALUE('month_dim'[month]),
			  SUM(monthly_net_worth[net_worth]),
			  CALCULATE(SUM(monthly_net_worth[net_worth]), LASTDATE('month_dim'[month]))
			)
		lineageTag: 4c1aa16c-8fd0-4b80-886b-96ef0b0d29b1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'NW Assets' =
			
			IF(HASONEVALUE('month_dim'[month]),
			  SUM(monthly_net_worth[assets]),
			  CALCULATE(SUM(monthly_net_worth[assets]), LASTDATE('month_dim'[month]))
			)
		lineageTag: 622a3381-1d36-49a4-88c8-c62107e76fd1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'NW Liabilities' =
			
			IF(HASONEVALUE('month_dim'[month]),
			  SUM(monthly_net_worth[liabilities]),
			  CALCULATE(SUM(monthly_net_worth[liabilities]), LASTDATE('month_dim'[month]))
			)
		lineageTag: 95322fc3-9beb-4380-9697-0499ffcc34f0

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'NW Liquid' =
			
			IF(HASONEVALUE('month_dim'[month]),
			  SUM(monthly_net_worth[liquid_net_worth]),
			  CALCULATE(SUM(monthly_net_worth[liquid_net_worth]), LASTDATE('month_dim'[month]))
			)
		lineageTag: f1d57415-df3b-4d19-ada3-8ce3355ebfcb

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'NW Investable' =
			
			IF(HASONEVALUE('month_dim'[month]),
			  SUM(monthly_net_worth[investable_assets]),
			  CALCULATE(SUM(monthly_net_worth[investable_assets]), LASTDATE('month_dim'[month]))
			)
		lineageTag: e201518e-f973-4223-8e87-629628f531e6

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'NW By Group (EOP)' =
			
			VAR LatestMonth = MAX('month_dim'[month])
			RETURN CALCULATE(SUM(monthly_net_worth_by_group[value]), 'month_dim'[month] = LatestMonth)
		lineageTag: 26969818-2935-4979-8d91-14ae3a5d71ec

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'NW Group %' = DIVIDE([NW By Group (EOP)], [NW Total])
		lineageTag: a8a505f0-5093-4b65-bfd5-a1f2cf863808

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Portfolio Value' = ```
			
			CALCULATE(
			  SUM(monthly_allocation[value]),
			  ALLEXCEPT(monthly_allocation, monthly_allocation[month])
			)
			
			```
		formatString: \$#,0.###############;(\$#,0.###############);\$#,0.###############
		lineageTag: 8b5a8cd6-1598-49d9-a889-a35b2345c84a

	measure 'Actual Weight' = ```
			
			DIVIDE(
			  SUM(monthly_allocation[value]),
			  [Portfolio Value]
			)
			
			```
		lineageTag: c0127e20-f434-4ce4-b9b9-03e0ad959cdb

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Target Weight' = AVERAGE(allocation_vs_target[target_weight])
		lineageTag: 415ac29b-877b-41ce-be1a-03af4ef09fea

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Variance Weight' = [Actual Weight] - [Target Weight]
		lineageTag: c750edd0-b2bb-4698-8ad9-e621480a441b

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Total Value' =
			
			SUM(positions_enriched_export[value])
		formatString: \$#,0;(\$#,0);\$#,0
		lineageTag: 794dcef8-09ac-4a05-9916-898fea4b15aa

	measure 'Portfolio Total (Selected)' =
			
			CALCULATE([Total Value], ALLSELECTED('security_dim'))
		lineageTag: a45a65dc-699e-4ce3-8841-fbd058d99cf4

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure '% of Portfolio' =
			
			DIVIDE([Total Value], [Portfolio Total (Selected)])
		formatString: 0.0%;-0.0%;0.0%
		lineageTag: c68d9077-a2b5-4263-9cde-01822ddb04a2

	measure 'Latest Month' =
			
			CALCULATE(MAX('month_dim'[month]), ALL('month_dim'))
		formatString: General Date
		lineageTag: 02c95118-b2c2-432e-a486-0480a408d5e8

	measure Trans_Amount_Total =
			
			SUM ( 'transactions_with_category'[amount] )
		lineageTag: e391dc71-41c1-48f1-a6ea-f9965d214777

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Trans_amount_month =
			
			AVERAGEX (
			    VALUES ( 'calendar_dim'[yyyymm] ),
			    CALCULATE ( [Trans_Amount_Total] )
			)
		formatString: \$#,0.0;(\$#,0.0);\$#,0.0
		lineageTag: de2d972d-4ea0-4952-ae09-5da35f4672e9

	measure 'Monthly Budget' =
			
			SUM ( budgets[Budget_Amount] )
		formatString: 0
		lineageTag: fa2a1d8c-bcc0-44c3-bf70-0b4529053811

	measure 'Budget to Date' = ```
			
			VAR LatestTxnDateInPeriod =
			    CALCULATE (
			        MAX ( transactions_with_category[Date] ),
			        -- keep the current month/quarter/year filters from month_dim
			        -- but IGNORE category filters so all rows use the same date
			        REMOVEFILTERS ( parent_category_dim ),
			        REMOVEFILTERS ( category_dim )
			    )
			
			VAR NoTxns = ISBLANK ( LatestTxnDateInPeriod )
			VAR LatestMonthStart =
			    DATE ( YEAR ( LatestTxnDateInPeriod ), MONTH ( LatestTxnDateInPeriod ), 1 )
			VAR DaysInLatestMonth = DAY ( EOMONTH ( LatestTxnDateInPeriod, 0 ) )
			VAR DaysElapsed       = DAY ( LatestTxnDateInPeriod )
			
			VAR FullMonthsBudget =
			    CALCULATE (
			        SUM ( budgets[Budget_Amount] ),
			        'month_dim'[MonthStart] < LatestMonthStart
			        -- category filters remain in context here (good)
			    )
			
			VAR LatestMonthBudget =
			    CALCULATE (
			        SUM ( budgets[Budget_Amount] ),
			        'month_dim'[MonthStart] = LatestMonthStart
			    )
			
			VAR ProratedLatest =
			    LatestMonthBudget * DIVIDE ( DaysElapsed, DaysInLatestMonth, 0 )
			
			RETURN
			IF ( NoTxns, BLANK(), FullMonthsBudget + ProratedLatest )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		lineageTag: 4cd00926-5c26-4e71-8523-876feff772c4

	measure 'Actual Spend (Outflows)' = - SUM ( transactions_with_category[Amount] )
		formatString: \$#,0;(\$#,0);\$#,0
		lineageTag: ad52acef-98a3-4d26-ae31-56a519768b5b

	measure 'Budget Variance' = [Budget to Date] - [Actual Spend (Outflows)]
		formatString: \$#,0;(\$#,0);\$#,0
		lineageTag: 8bb69587-f26d-47d3-a515-6f604d8ca0af

	measure 'Dividends (Net)' =
			
			SUM ( DivFlowsYear[dividends_net_by_year] )
		formatString: \$#,0;(\$#,0);\$#,0
		lineageTag: 91c077e1-69c9-4669-ae29-d3ffc26ee48a

	measure 'Net Flow (w/ Dividends)' =
			
			[Net Flow (cash only)] + [Dividends (Net, Year)]
		formatString: \$#,0;(\$#,0);\$#,0
		lineageTag: 0f99c244-5c01-4734-bc15-397bcd64ad5f

	measure Inflows =
			
			CALCULATE ( SUM ( Cashflows[Value] ), Cashflows[Type] = "Inflows" )
		lineageTag: acdf2bf0-cc76-4b0f-bc12-274c7000654f

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Outflows =
			
			CALCULATE ( SUM ( Cashflows[Value] ), Cashflows[Type] = "Outflows" )
		lineageTag: 2bd256fb-78e3-47d9-acbc-7f0234e491c5

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Dividends (Net, Year)' =
			
			SUM ( DivFlowsYear[dividends_net_by_year] )
		lineageTag: 4a4dc4fd-7011-437e-b983-01b35fbe4f59

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Net Flow (cash only)' =
			
			SUM ( Cashflows[SignedValue] )
		lineageTag: 0bd49e0c-d0e2-4bcd-b135-1e9e2ddf47a9

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'End Balance (EOY)' = ```
			
			VAR y = MAX(YearTable[Year])
			VAR r = COALESCE([Return ex Div (Nominal)], 0)
			
			VAR baseYear =
			    VAR cy = CALCULATE(MAX(ret_globals[Value]), ret_globals[Variable] = "current_year")
			    RETURN IF(NOT ISBLANK(cy), cy, YEAR(TODAY()))
			
			VAR StartCompounded =
			    [Start Balance] * POWER(1 + r, y - baseYear)
			
			/* Include BOTH inflows and outflows from Cashflows; exclude dividends here
			   because dividends are added from DivFlowsYear below. */
			VAR Flows_NoDiv =
			    SUMX(
			        FILTER(
			            ALL(Cashflows),
			            VALUE(Cashflows[Year]) <= y &&
			            ( LOWER(Cashflows[Type]) = "inflows" || LOWER(Cashflows[Type]) = "outflows" )
			        ),
			        Cashflows[SignedValue] * POWER(1 + r, y - VALUE(Cashflows[Year]))
			    )
			
			/* Net dividends (cash) compounded forward */
			VAR Divs_Compounded =
			    SUMX(
			        FILTER(ALL(DivFlowsYear), VALUE(DivFlowsYear[year]) <= y),
			        DivFlowsYear[dividends_net_by_year] * POWER(1 + r, y - VALUE(DivFlowsYear[year]))
			    )
			
			RETURN
			    StartCompounded + Flows_NoDiv + Divs_Compounded
			
			```
		lineageTag: 50049610-f0e7-4c92-a90c-a4093fb6054f

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Beg Balance' = ```
			
			VAR y =
			    MAX(YearTable[Year])
			VAR baseYear =
			    VAR cy =
			        CALCULATE(MAX(ret_globals[Value]), ret_globals[Variable] = "current_year")
			    RETURN IF(NOT ISBLANK(cy), cy, YEAR(TODAY()))
			RETURN
			IF(
			    y = baseYear,
			    [Start Balance],
			    CALCULATE(
			        [End Balance (EOY)],
			        FILTER(ALL(YearTable[Year]), YearTable[Year] = y - 1)
			    )
			)
			
			```
		lineageTag: 79e40321-2c1c-4041-b313-eca21533c226

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Amount By Type' = ```
			
			VAR sel = SELECTEDVALUE(TypeDim[Type])
			RETURN
			SWITCH(
			    sel,
			    "Inflows",   CALCULATE(SUM(Cashflows[SignedValue]),  Cashflows[Type] = "Inflows"),
			    "Outflows",  CALCULATE(SUM(Cashflows[SignedValue]),  Cashflows[Type] = "Outflows"),
			    "Dividends", SUM(DivFlowsYear[dividends_net_by_year]),
			    "Beg Balance", [Beg Balance],
			    "End Balance", [End Balance (EOY)],
			    BLANK()
			)
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		lineageTag: 0309dd80-c8bb-4f41-aec1-167f706ad605

	measure 'Return ex Div (Real)' = ```
			
			VAR r_total_real = COALESCE([Real Return Rate], 0)           -- your total real return (incl. dividends)
			VAR y_div_net    = COALESCE([Dividend Yield (Net, Real)], 0) -- matches the net cash you model in flows
			RETURN (1 + r_total_real) / (1 + y_div_net) - 1
			
			
			```
		lineageTag: 1e4a42d8-d669-4abc-8299-3f3f38b963c6

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Dividend Yield (Net, Real)' = ```
			
			DIVIDE(
			    SUM(DivFlowsYear[dividends_net_by_year]),
			    SUM(DivFlowsYear[portfolio_value])
			)
			
			```
		formatString: 0
		lineageTag: 1b296550-8f9c-4e6d-825a-3ea85cf85611

	measure 'Nominal Total Return' = ```
			
			VAR r_real = COALESCE([Real Return Rate], 0)   -- e.g., 0.04
			VAR i      = COALESCE([Inflation Rate], 0)    -- e.g., 0.03
			RETURN (1 + r_real) * (1 + i) - 1
			
			```
		lineageTag: fab2812b-620e-4f0a-9572-25c722a2ea4c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Return ex Div (Nominal)' = ```
			
			VAR r_nom_total = COALESCE([Nominal Total Return], 0)
			VAR y_div_net   = COALESCE([Dividend Yield (Net, Real)], 0)  -- okay to use this; difference vs nominal yield is tiny
			RETURN (1 + r_nom_total) / (1 + y_div_net) - 1
			
			```
		lineageTag: 53264fdf-6fbf-4418-99e2-2b40fb5a03e5

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Outflows (Signed)' = ```
			
			SUMX(
			    FILTER(Cashflows, LOWER(Cashflows[Type]) = "outflows"),
			    Cashflows[SignedValue]                -- should be negative
			)
			
			```
		lineageTag: 8d2c3fde-fc77-41f6-879a-db95dde7aa3e

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Outflows (Abs)' = ```
			ABS( [Outflows (Signed)] )
			
			```
		lineageTag: be1dc4e6-4f61-4843-9211-cae34dc9f36a

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Avg Withdrawal Rate (Weighted)' =
			
			VAR YearsWithW =
			    FILTER( VALUES(YearTable[Year]), CALCULATE([Outflows (Abs)]) > 0 && CALCULATE([Beg Balance]) > 0 )
			RETURN
			DIVIDE(
			    SUMX(YearsWithW, CALCULATE([Outflows (Abs)])),
			    SUMX(YearsWithW, CALCULATE([Beg Balance]))
			)
		formatString: 0.0%;-0.0%;0.0%
		lineageTag: 24b46ad3-a4c0-4b9c-8a2f-ec8733a11ccd

	measure 'Net Worth (EOY)' = [End Balance (EOY)]
		formatString: \$#,0;(\$#,0);\$#,0
		lineageTag: 8125eb16-c3d4-48f8-b3ca-a367d055a594

	column Column1
		dataType: int64
		isHidden
		formatString: 0
		lineageTag: db8b395b-7408-4703-bf41-a87dd22a80b7
		summarizeBy: sum
		sourceColumn: Column1

		changedProperty = IsHidden

		annotation SummarizationSetBy = Automatic

	partition Measures_Main = m
		mode: import
		source =
				let
				    Source = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i45WMlCKjQUA", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [Column1 = _t]),
				    #"Changed Type" = Table.TransformColumnTypes(Source,{{"Column1", Int64.Type}})
				in
				    #"Changed Type"

	annotation PBI_ResultType = Table

